configure(subprojects.findAll {
    if (it.name.startsWith("sample")) {
        return false
    }

    if (it.name.startsWith("testonly")) {
        return false
    }

    if ("true" == System.getProperty("publishNikeInternalUtil")) {
        return it.name == "nike-internal-util"
    }

    return it.name != "nike-internal-util"
}) {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    ext {
        def isNikeInternalUtilProject = project.getName() == "nike-internal-util"
        createPomOutputLocation = "${project.buildDir}/libs/pom.xml"
        descriptionForPom = (isNikeInternalUtilProject) ? "Nike internal utilities library" : "Backstopper module ${project.getName()}"
        bintrayPackage = (isNikeInternalUtilProject) ? "internal" : "backstopper"
    }

    task createPom {
        doLast {
            pom {
                project {
                    groupId groupId
                    artifactId artifactId
                    version version
                    name artifactId
                    description descriptionForPom
                    url 'https://github.com/Nike-Inc/backstopper'
                    inceptionYear '2016'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    scm {
                        url 'https://github.com/Nike-Inc/backstopper'
                        connection 'git@github.com:Nike-Inc/backstopper.git'
                    }
                    developers {
                        developer {
                            id 'nicmunroe'
                            name 'Nic Munroe'
                        }
                    }
                    organization {
                        name 'Nike'
                        url 'http://nike-inc.github.io'
                    }
                }
            }.writeTo(createPomOutputLocation)
        }
    }

    publishing {
        publications {
            mavenInfo(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom.withXml {
                    // The pom produced by this task is questionable - ALL dependencies are set to runtime rather than how we set them up in the build.gradle files.
                    //      Unfortunately we need to use this task anyway for the bintray gradle plugin.
                    //      Therefore we'll just wipe out the pom this produces and use one created by the normal maven plugin (see the createPom task) instead.
                    asString().delete(0, Integer.MAX_VALUE).append(new File(createPomOutputLocation).text)
                }
            }
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey
        dryRun = false
        publish = false
        publications = ['mavenInfo']
        pkg {
            repo = 'maven'
            name = bintrayPackage
            userOrg = 'nike'
            licenses = ['Apache-2.0']
            websiteUrl = 'https://github.com/Nike-Inc/backstopper'
            issueTrackerUrl = 'https://github.com/Nike-Inc/backstopper/issues'
            vcsUrl = 'https://github.com/Nike-Inc/backstopper.git'
            githubRepo = 'Nike-Inc/backstopper'
            githubReleaseNotesFile = 'CHANGELOG.md'

            version {
                name = bintrayVersion

                gpg {
                    sign = true
                }
            }
        }
    }

    bintrayUpload.dependsOn(createPom)
    assemble.finalizedBy(createPom)
}